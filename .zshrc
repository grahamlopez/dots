##############################################################
#
# orig .zshrc
#
#################################################################

#################################################################
# general options                                             {{{
#################################################################

# Add completions only if they exist on this machine. /some/path/to/dir(N/)
# matches a 'dir/' directory, but not recursively, and "N" drops the path when
# there is no match, instead of leaving a dead path sitting in fpath.
fpath=(~/.zsh/completion(N/) ~/.zsh/completion/conda-zsh-completion(N/) $fpath)

# completion system (see 20.2.1 of the zsh manual)
autoload -Uz compinit
compinit -i

# turn off all terminal beeps
unsetopt BEEP

export HISTSIZE=10000
export SAVEHIST=$HISTSIZE
export HISTFILE="$HOME/.zsh.history"
setopt EXTENDED_HISTORY          # Write the history file in the ":start:elapsed;command" format.
setopt SHARE_HISTORY             # Share history between all sessions.
setopt HIST_EXPIRE_DUPS_FIRST    # Expire duplicate entries first when trimming history.
setopt HIST_IGNORE_DUPS          # Don't record an entry that was just recorded again.
#setopt BANG_HIST                 # Treat the '!' character specially during expansion.
#setopt INC_APPEND_HISTORY        # Write to the history file immediately, not when the shell exits.
#setopt HIST_IGNORE_ALL_DUPS      # Delete old recorded entry if new entry is a duplicate.
#setopt HIST_FIND_NO_DUPS         # Do not display a line previously found.
#setopt HIST_IGNORE_SPACE         # Don't record an entry starting with a space.
#setopt HIST_SAVE_NO_DUPS         # Don't write duplicate entries in the history file.
#setopt HIST_REDUCE_BLANKS        # Remove superfluous blanks before recording entry.
#setopt HIST_VERIFY               # Don't execute immediately upon history expansion.
#setopt HIST_BEEP                 # Beep when accessing nonexistent history.

export SHELL=${commands[zsh]}   # zsh's own command table; no subprocess
export EDITOR=nvim
export VISUAL=nvim
# Single source of truth for less. -R passes color through, which the old
# '-ifqm' lacked - anything piping color to less without the alias showed
# raw escape codes. MANPAGER below adds only what is man-specific.
export LESS='-ifqMR'
export PAGER='less'

umask 077
limit coredumpsize 0        # Turn off core dumps

# report time for long-running commands (based on system+user time)
# can also notify with some hackery: https://superuser.com/a/578651
export REPORTTIME=5

#################################################################
# }}}
#################################################################




#################################################################
# specific environments                                       {{{
#################################################################

# disable gnome/kde-ssh keyring nonsense on remote servers
[ -n "$SSH_CONNECTION" ] && unset SSH_ASKPASS

# -U keeps these arrays de-duplicated, so re-sourcing this file (or a nested
# shell) cannot stack the same directory onto PATH twice.
typeset -U path PATH
export PATH="${HOME}/local/bin:${PATH}"
export GOBIN="${HOME}/local/bin"
export PATH="${HOME}/.local/bin:${PATH}"

#################################################################
# }}}
#################################################################




#################################################################
# keybindings, aliases, functions, abbreviations              {{{
#################################################################

# display existing key bindings with `bindkey`

# reset to emacs style
bindkey -e

# this is to get bash-style word treatment: most importantly, kill-word only
# goes to the directory delimeter, instead of killing the entire path
autoload -U select-word-style
select-word-style bash

bindkey '^D' kill-word
bindkey '^B' backward-word
bindkey '^F' forward-word

# for quick alternate command
# - can also use ^U (cut) ^Y (paste) as well
bindkey '^K' push-line
bindkey '^R' history-incremental-search-backward
bindkey '^Y' yank
bindkey '^U' kill-whole-line

# general commands defaults
alias ls="ls --color=auto -F"
alias grep="grep --color=auto"
# $+commands[x] is zsh's own lookup: no subprocess, no quoting hazard
(( $+commands[nvim] )) && alias vim="nvim"
alias vimall="nvim **/*(.)"

# dotfile management
alias dgit='git --git-dir=$HOME/.dots-git/ --work-tree=$HOME'
alias ggit='git --git-dir=$HOME/.dots-gui-git/ --work-tree=$HOME'
alias agit='git --git-dir=$HOME/.dots-ai-git/ --work-tree=$HOME'
alias gs='$HOME/.utils/git-status-all.sh --fetch'

# ssh-agent on a fixed socket, reused across shells.
# Starts a *keyless* agent if none is reachable - no passphrase prompt here.
# Keys load on first actual connection via AddKeysToAgent in ~/.ssh/config.
export SSH_AUTH_SOCK="${HOME}/.ssh/agent.sock"
ssh-add -l >/dev/null 2>&1
_ssh_agent_rc=$?               # capture immediately; 2 = no agent reachable
if [ ${_ssh_agent_rc} -eq 2 ]; then
    rm -f "${SSH_AUTH_SOCK}"   # clear a stale socket left by a dead agent
    ssh-agent -a "${SSH_AUTH_SOCK}" >/dev/null 2>&1
fi
unset _ssh_agent_rc

# graphical environment related settings
function brightness_set () {
  local max val backpath

  if [ -d /sys/class/backlight/intel_backlight ]; then # flattop, nvgen
    backpath=/sys/class/backlight/intel_backlight
  elif [ -d /sys/class/backlight/acpi_video0 ]; then # startop
    backpath=/sys/class/backlight/acpi_video0
  else
    echo "cannot find backlight" >&2
    return 1
  fi

  max=$(< ${backpath}/max_brightness) || {
    echo "Cannot read max_brightness" >&2
    return 1
  }

  case "$1" in
    1) val=$(( max * 1  / 100 )) ;;   # 1%
    2) val=$(( max * 5  / 100 )) ;;   # 5%
    3) val=$(( max * 10 / 100 )) ;;   # 10%
    4) val=$(( max * 25 / 100 )) ;;   # 25%
    5) val=$(( max * 40 / 100 )) ;;   # 40%
    6) val=$(( max * 60 / 100 )) ;;   # 60%
    7) val=$(( max * 80 / 100 )) ;;   # 80%
    8) val=$(( max * 100 / 100 )) ;;  # 100%
    *) echo "provide setting level 1-8" ; return 1 ;;
  esac

  echo "$val" > ${backpath}/brightness
}

function dark_theme() {
  gsettings set org.gnome.desktop.interface color-scheme 'prefer-dark'
}

function light_theme() {
  gsettings set org.gnome.desktop.interface color-scheme 'prefer-light'
}

function reset_hypr_scaling() {
  hyprctl keyword monitor "eDP-1,preferred,auto,1.33"
}

# launch kitty to trust remote ssh hosts with our local clipboard contents
alias kitty_trusted='kitty -o clipboard_control="write-clipboard write-primary read-clipboard read-primary no-ask"'

function opacity_kitty_toggle () {
  if [ -n "$TMUX" ] ; then
    echo "Opacity toggle disabled inside tmux" >&2
    return 1
  fi
  kitty @ --to ${KITTY_LISTEN_ON} set-background-opacity --toggle 1.0
}

# messing with neovim
alias cvim="NVIM_APPNAME=nvim.cprog nvim"
alias ovim="NVIM_APPNAME=nvim.old nvim"

function uninstall_nvim() {
  if [ -z "$1" ]; then
    echo "uninstalling main nvim state"
    rm -rf ${HOME}/.local/share/nvim
    rm -rf ${HOME}/.local/state/nvim
    rm -rf ${HOME}/.cache/nvim
  else
    echo "uninstalling nvim.$1 state"
    rm -rf ${HOME}/.local/share/nvim.$1
    rm -rf ${HOME}/.local/state/nvim.$1
    rm -rf ${HOME}/.cache/nvim.$1
  fi
}


# more homegrown functions

# bit-perfect compare two directories
function bit_diff_dirs {
    src=$1
    dst=$2
    (cd ${src}  && find . -type f -print0 | xargs -0 sha256sum | sort -k2 > /tmp/src.sha256)
    (cd ${dst}  && find . -type f -print0 | xargs -0 sha256sum | sort -k2 > /tmp/dst.sha256)
    diff --color /tmp/src.sha256 /tmp/dst.sha256
}

# abbreviations and magic expansion
# obtained from stackoverflow (but the link now redirects incorrectly)
setopt extendedglob
typeset -Ag abbreviations
abbreviations=(
  "lxe"   "lxc exec __CURSOR__ -- sudo --login --user ubuntu"
)

magic-abbrev-expand() {
    local MATCH
    LBUFFER=${LBUFFER%%(#m)[_a-zA-Z0-9]#}
    command=${abbreviations[$MATCH]}
    LBUFFER+=${command:-$MATCH}

    if [[ "${command}" =~ "__CURSOR__" ]]
    then
        RBUFFER=${LBUFFER[(ws:__CURSOR__:)2]}
        LBUFFER=${LBUFFER[(ws:__CURSOR__:)1]}
    else
        zle self-insert
    fi
}

no-magic-abbrev-expand() {
  LBUFFER+=' '
}

zle -N magic-abbrev-expand
zle -N no-magic-abbrev-expand
bindkey " " magic-abbrev-expand
bindkey "^x " no-magic-abbrev-expand
bindkey -M isearch " " self-insert

#################################################################
# }}}
#################################################################




#################################################################
# Appearance and prompt                                       {{{
#################################################################

# Start from dircolors' built-in defaults, then re-apply my palette on top.
# Later keys win, so these overrides beat the stock values.
#
# Why not a ~/.dir_colors file: dircolors only emits LS_COLORS when TERM
# matches one of the file's TERM globs. The old solarized file was written for
# coreutils 5.97 and lists no xterm-kitty (and no COLORTERM catch-all), so it
# silently produced an EMPTY LS_COLORS - no colour at all - outside tmux.
# The built-in defaults cover modern terminals; only the palette was worth keeping.
#
# Palette: dirs cyan, symlinks magenta, executables red, source/text green,
# archives magenta, media yellow, backups cyan. The stock ow/tw/st/su/sg
# entries are cleared because they use unreadable coloured backgrounds
# (other-writable dirs default to blue-on-green).
eval "$(dircolors -b)"
LS_COLORS+=":di=36:ln=35:ex=01;31:ow=:tw=:st=:su=:sg="
for _e in c h cc cpp cxx hpp py sh zsh bash pl pm rb js ts jsx html htm css scss \
          md org tex txt vim lua el conf cfg ini yml yaml toml json xml; do
  LS_COLORS+=":*.$_e=32"
done
for _e in tar tgz tbz tbz2 gz bz bz2 xz zst zip 7z rar arj cab deb rpm jar war \
          apk iso dmg msi gem Z z; do
  LS_COLORS+=":*.$_e=1;35"
done
for _e in png jpg jpeg gif bmp tif tiff svg svgz xcf xpm ppm pgm pbm webp \
          mp4 m4v mkv avi mov mpg mpeg webm wmv flv ogv ogm vob qt rm rmvb asf \
          mp3 flac wav aac ogg opus mid midi mka ra au pdf ps eps; do
  LS_COLORS+=":*.$_e=33"
done
for _e in bak old orig swp swo dist; do
  LS_COLORS+=":*.$_e=01;36"
done
unset _e
export LS_COLORS

# colored completion - use my LS_COLORS
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}

# Color shortcuts. $'\e' rather than a literal ESC byte, so these lines stay
# readable in diffs, pagers and web views (same idiom as LESS_TERMCAP_* below).
# The %{...%} pairs tell zsh the escape occupies zero display columns; the
# A* variants are bare ANSI, for echo/printf rather than the prompt.
# PROMPT/RPROMPT currently use only RED GREEN YELLOW CYAN WHITE NOCOLOR;
# the others are kept for reference.
  RED=$'%{\e[0;31m%}'
  LIGHTRED=$'%{\e[1;31m%}'

  GREEN=$'%{\e[0;32m%}'
  LIGHTGREEN=$'%{\e[1;32m%}'

  YELLOW=$'%{\e[0;33m%}'
  LIGHTYELLOW=$'%{\e[1;33m%}'

  BLUE=$'%{\e[0;34m%}'
  LIGHTBLUE=$'%{\e[1;34m%}'

  PURPLE=$'%{\e[0;35m%}'
  LIGHTPURPLE=$'%{\e[1;35m%}'

  CYAN=$'%{\e[0;36m%}'
  LIGHTCYAN=$'%{\e[1;36m%}'

  GRAY=$'%{\e[1;30m%}'
  LIGHTGRAY=$'%{\e[0;37m%}'

  WHITE=$'%{\e[0;37m%}'
  LIGHTWHITE=$'%{\e[1;37m%}'

  NOCOLOR=$'%{\e[0m%}'

  ARED=$'\e[0;31m'
  ANOCOLOR=$'\e[0;0m'
  AGREEN=$'\e[0;32m'
  ABLUE=$'\e[0;34m'
  ACYAN=$'\e[0;36m'
  APURPLE=$'\e[0;35m'

# for git repo information. Prefer the copy shipped with git (so it updates
# when git does), falling back to the vendored one on machines whose packaging
# omits it. Paths differ by distro; first readable hit wins.
for _gp in /usr/share/git/git-prompt.sh \
           /usr/share/git-core/contrib/completion/git-prompt.sh \
           /usr/share/git/completion/git-prompt.sh \
           /usr/lib/git-core/git-sh-prompt \
           ~/.zsh/git-prompt.sh; do
  [[ -r $_gp ]] && source $_gp && break
done
unset _gp
setopt prompt_subst

export GIT_PS1_SHOWDIRTYSTATE=true
export GIT_PS1_SHOWUPSTREAM="auto"
export GIT_PS1_SHOWCOLORHINTS=true

# The dotfiles bare repo has core.worktree=$HOME, so git can resolve $HOME
# itself as a work tree and the prompt then reports the dots repo while you
# are just sitting at home. Suppress it in that one directory; subdirectories
# and every real repo are unaffected.
_git_ps1() {
  [[ $PWD == $HOME ]] && return
  __git_ps1 "$@"
}

PROMPT='$GREEN%m$CYAN:$CYAN%3~$YELLOW$(_git_ps1 "(%s)")$CYAN-| $NOCOLOR'
# PROMPT='$RED%m$CYAN:$CYAN%3~$YELLOW$(_git_ps1 "(%s)")$CYAN-| $NOCOLOR' # root

RPROMPT='$RED%(?..[%?]) $CYAN|$WHITE%*$NOCOLOR'

export LESS_TERMCAP_mb=$'\e[1;31m'     # begin bold
export LESS_TERMCAP_md=$'\e[1;33m'     # begin blink
export LESS_TERMCAP_so=$'\e[01;44;37m' # begin reverse video
export LESS_TERMCAP_us=$'\e[01;37m'    # begin underline
export LESS_TERMCAP_me=$'\e[0m'        # reset bold/blink
export LESS_TERMCAP_se=$'\e[0m'        # reset reverse video
export LESS_TERMCAP_ue=$'\e[0m'        # reset underline
export GROFF_NO_SGR=1                  # for konsole and gnome-terminal

# inherits $LESS; -s squeezes blank lines, +Gg is the man line-count trick
export MANPAGER='less -s +Gg'

#################################################################
# }}}
#################################################################


#################################################################
# Auto installed stuff                                        {{{
#################################################################

# Intentional: the -s guards make this a no-op on machines without ~/.nvm,
# and load nvm on the ones that have it. Not dead code - leave it.
export NVM_DIR="$HOME/.nvm"
[ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh"  # This loads nvm
[ -s "$NVM_DIR/bash_completion" ] && \. "$NVM_DIR/bash_completion"  # This loads nvm bash_completion
